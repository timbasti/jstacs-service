plugins {
	id 'org.springframework.boot' version '2.4.0'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'
	id 'org.asciidoctor.convert' version '1.5.8'
	id 'java'
	id 'war'
}

group = 'de.jstacs'
version = '1.0.0-SNAPSHOT'
sourceCompatibility = '11'

repositories {
	mavenCentral()
}

ext {
	set('snippetsDir', file("build/generated-snippets"))
}

configurations {
	developmentOnly
	runtimeClasspath {
		extendsFrom developmentOnly
	}
}

dependencies {
	asciidoctor 'org.springframework.restdocs:spring-restdocs-asciidoctor'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	implementation 'com.cedarsoftware:json-io:4.12.0'
	implementation 'com.google.guava:guava:30.0-jre'
    implementation 'org.apache.tika:tika-core:1.25'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation fileTree('lib') {include '*.jar'}
	providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
}

test {
	outputs.dir snippetsDir
	useJUnitPlatform()
}

asciidoctor {
	sourceDir  file('src/docs')
	outputDir  file('build/docs')
	dependsOn test
}

task createProperties(dependsOn: processResources) {
  doLast {
    new File("$buildDir/resources/main/service.properties").withWriter { w ->
        Properties p = new Properties()
        p['version'] = project.version.toString()
		p['name'] = project.name.toString()
        p.store w, null
    }
  }
}

classes {
    dependsOn createProperties
}

// To deploy on heroku
task stage(type: Copy, dependsOn: [clean, bootJar]) {
    from jar.archivePath
    into project.rootDir 
    rename {
        'app.jar'
    }
    mustRunAfter clean
}

clean {
    doLast {
        project.file('app.jar').delete()
    }
}
